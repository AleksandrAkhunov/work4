
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
	    Возврат;
	КонецЕсли; 
	СуммаДокумента = Товары.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	// регистр Продажи 
	Для Каждого СтрокаТоваров ИЗ Товары Цикл
		Движения.Продажи.Записывать = Истина;
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Товар = СтрокаТоваров.Товар;
		Движение.Акция = Акция;
		Движение.Контрагент = Контрагент;
		Движение.Количество = СтрокаТоваров.Количество;
		Движение.СуммаПродажи = СтрокаТоваров.Сумма;
	КонецЦикла; 
	
	Бл = Новый БлокировкаДанных;
	ЭлБл = Бл.Добавить("РегистрНакопления.ТоварыНаСкладах");
	ЭлБл.ИсточникДанных = Товары;
	ЭлБл.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлБл.ИспользоватьИзИсточникаДанных("Товар", "Товар");
	Бл.Заблокировать();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|Товары.Товар,
	|СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ Документ.Продажа.Товары КАК Товары
	|ГДЕ Товары.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО Товары.Товар;
	|ВЫБРАТЬ
	|Товары.Товар,
	|Товары.Количество,
	|ЕСТЬNULL(Остатки.КоличествоОстаток,0) КАК Остаток
	|ИЗ Товары
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(&МоментВремени, Товар В (ВЫБРАТЬ Товар ИЗ Товары)) КАК Остатки
	|ПО Товары.Товар = Остатки.Товар
	|";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
	    Если Выборка.Количество > Выборка.Остаток Тогда
		    Отказ = Истина;
			Сообщить("Для продажи недостаточно " + Строка(Выборка.Количество - Выборка.Остаток) + " шт товара " + Строка(Выборка.Товар));
		Иначе
			Движения.ТоварыНаСкладах.Записывать = Истина;
			Движение = Движения.ТоварыНаСкладах.Добавить();
			Движение.Активность = Истина;
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			ЗаполнитьЗначенияСвойств(Движение, Выборка, "Товар,Количество");
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры
